set( _iscool_core_boost_libraries system )

if( NOT iscool-core_FIND_COMPONENTS
    OR "locale" IN_LIST iscool-core_FIND_COMPONENTS )
  set(
    _iscool_core_boost_libraries
    ${_iscool_core_boost_libraries} locale
    )
  find_package( ICU REQUIRED data i18n io tu uc )
endif()

find_package(
  Boost @required_boost_version@ REQUIRED COMPONENTS
  ${_iscool_core_boost_libraries}
  )

if( NOT iscool-core_FIND_COMPONENTS
    OR "http" IN_LIST iscool-core_FIND_COMPONENTS
    OR "json" IN_LIST iscool-core_FIND_COMPONENTS
    OR "preferences" IN_LIST iscool-core_FIND_COMPONENTS
    OR "resources" IN_LIST iscool-core_FIND_COMPONENTS
    OR "style" IN_LIST iscool-core_FIND_COMPONENTS )
  find_package( jsoncpp )
endif()

if( NOT iscool-core_FIND_COMPONENTS
    OR "i18n" IN_LIST iscool-core_FIND_COMPONENTS )
  find_package( moFileReader )
endif()

if( NOT iscool-core_FIND_COMPONENTS
    OR "video" IN_LIST iscool-core_FIND_COMPONENTS )
  find_library( avcodec_library avcodec REQUIRED )
  find_library( avformat_library avformat REQUIRED )
  find_library( avutil_library avutil REQUIRED )
  find_library( swscale_library swscale REQUIRED )

  find_package( ZLIB )
  find_package( Threads REQUIRED )
endif()

get_filename_component( CURRENT_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH )
include( ${CURRENT_DIR}/iscool-core-config-generated.cmake )
